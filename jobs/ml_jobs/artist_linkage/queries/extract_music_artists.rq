PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <http://schema.org/>

SELECT
  ?wikidata_id
  ?artist_name_fr
  ?artist_name_en
  ?artist_description
  ?wikipedia_url
  ?img
  ?gkg_id
  (GROUP_CONCAT(DISTINCT(?alias_fr); separator=" | ") AS ?aliases_fr)
  (GROUP_CONCAT(DISTINCT(?alias_en); separator=" | ") AS ?aliases_en)
  ((IF(BOUND(?genius_id) && ?genius_id != "", 1, 0) + IF(BOUND(?spotify_id) && ?spotify_id != "", 1, 0) + IF(BOUND(?deezer_id) && ?deezer_id != "", 1, 0)) AS ?matching_score)
WHERE {
  {
    ?wikidata_id wdt:P31 wd:Q5  # Human
  }
  UNION
  {
    ?wikidata_id wdt:P31 wd:Q215380  # Musical group
  }
  UNION
  {
    ?wikidata_id wdt:P31 wd:Q216337  # Boy band
  }
  UNION
  {
    ?wikidata_id wdt:P31 wd:Q641066 # Girl band
  }
  OPTIONAL { ?wikidata_id schema:description ?artist_description . FILTER (LANG(?artist_description) = "fr") . }
  OPTIONAL { ?wikidata_id rdfs:label ?label_fr. FILTER(LANG(?label_fr) = "fr") }
  OPTIONAL { ?wikidata_id rdfs:label ?label_mul. FILTER(LANG(?label_mul) = "mul") }
  BIND(COALESCE(?label_fr, ?label_mul) AS ?artist_name_fr)
  OPTIONAL { ?wikidata_id rdfs:label ?artist_name_en. FILTER(LANG(?artist_name_en) = "en") }
  OPTIONAL { ?wikidata_id skos:altLabel ?alias_fr . FILTER (lang(?alias_fr) = "fr") }
  OPTIONAL { ?wikidata_id skos:altLabel ?alias_en . FILTER (lang(?alias_en) = "en") }
  OPTIONAL {
      ?wikipedia_url_fr schema:about ?wikidata_id .
      ?wikipedia_url_fr schema:isPartOf <https://fr.wikipedia.org/> .
  }
  OPTIONAL {
      ?wikipedia_url_en schema:about ?wikidata_id .
      ?wikipedia_url_en schema:isPartOf <https://en.wikipedia.org/> .
  }
  BIND(COALESCE(?wikipedia_url_fr, ?wikipedia_url_en) AS ?wikipedia_url)
  OPTIONAL {?wikidata_id wdt:P2373 ?genius_id . }
  OPTIONAL {?wikidata_id wdt:P1902 ?spotify_id . }
  OPTIONAL {?wikidata_id wdt:P2722 ?deezer_id . }
  OPTIONAL {?wikidata_id wdt:P18 ?img . }
  OPTIONAL {?wikidata_id wdt:P2671 ?gkg_id .}
  FILTER ((?artist_name_fr != "") || (?artist_name_en != ""))
  FILTER ((?genius_id != "" ) || (?spotify_id != "") || (?deezer_id != ""))
}
GROUP BY ?wikidata_id ?artist_name_fr ?artist_name_en ?artist_description ?wikipedia_url ?img ?gkg_id ?genius_id ?spotify_id ?deezer_id
